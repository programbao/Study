! ===========================================第一种方法=====================================
finish    !基础步骤一：结束上一个工作状态
/clear    !基础步骤二：清空当前的数据库
!基础步骤三：文件命名（做测试可以不命名，也可以命名；做正式建模要命名）
/prep7    !基础步骤三：进入前处理板块
!基础步骤4：材料和单元的定义
!========================单元定义
et,1,solid186
et,2,surf154
!========================材料的参数定义
mp,ex,1,2.1e11
mp,prxy,1,0.2       !泊松比
!参数定义
r1 = 2.25   !内圈半径
r2 = 4.5    !外圈半径
h = 17      !径道长度（高度）
d = 3.4     !拉伸体的厚度
press = 400e6
div1 = 6
div2 = 40
arc_div1 = 20
!建模操作
!================第一个半弧面
!建点
k,1,-r2,0.5*h,0
k,2,-r1,0.5*h,0
k,3,r1,0.5*h,0
k,4,r2,0.5*h,0
k,5,0,0.5*h + r1,0
k,6,0,0.5*h + r2,0
!连接线
l,1,2
l,3,4
larc,2,3,5,r1
larc,1,4,6,r2
!创建面----al根据线创建面
al,1,3,2,4
!================第二个半弧面
!建点
k,7,-r2,-0.5*h,0
k,8,-r1,-0.5*h,0
k,9,r1,-0.5*h,0
k,10,r2,-0.5*h,0
k,11,0,-0.5*h - r1,0
k,12,0,-0.5*h - r2,0
!连接线
l,7,8
l,9,10
larc,8,9,11,r1
larc,7,10,12,r2
!创建面----al根据线创建面
al,5,7,6,8
!=====================创建第一个柱面
!连接关键点
! l,1,7
! l,2,8
! l,3,9
! l,4,10
! !创建面
! al,1,9,5,10
! al,2,11,6,12
!上面可以使用BLC4创建矩形面的命令来简化为
BLC4,-1*r2,-0.5*h,r1,h
BLC4,r1,-0.5*h,r1,h
! 让所有面间相互粘接
Aglue,all
!======================根据面创建体
!得到最大编号
*get,kp_num,kp,0,num,maxd
k,kp_num + 1,-r2,0.5*h,d
l,1,kp_num + 1 
! 拉伸体
LL1 = _return
allsel,all
vdrag,all,,,,,,LL1
! ===============================
! 划分网格
!=== 根据线来划分---首先先选择线lsel
lsel,s,loc,y,0.5*h
lsel,a,loc,y,-0.5*h
lsel,r,loc,z,0
lesize,all,,,div1     ! 线的大小划分（段数）
!=== 选择竖向线段
lsel,s,loc,x,r1
lsel,a,loc,x,r2
lsel,a,loc,x,-r1
lsel,a,loc,x,-r2
lsel,r,loc,z,0
lesize,all,,,div2  !划分段数
!==== 选择半弧
lsel,s,loc,y,0.5*h + 0.1,0.5*h + r2
lsel,a,loc,y,- 0.5*h - 0.1,- 0.5*h - r2
lsel,r,loc,z,0
! 划分半弧大小（段数）
lesize,all,,,arc_div1

! ===深度方向划分
! lsel,s,loc,z,0 + 0.1,3.4 - 0.1
! lsel,u,loc,x,r2
! lsel,u,loc,x,-r1
! lesize,all,,,10
lsel,a,loc,z,0 + 0.1,3.4 - 0.1
esize,d/10
allsel,all
vsweep,all








! =========================第二种方法=================
finish    
/clear    
/prep7    
et,1,solid186
et,2,surf154
mp,ex,1,2.1e11
mp,prxy,1,0.3      
r1 = 2.25   
r2 = 4.5    
h = 17      
d = 3.4     
div1 = 6
div2 = 40
arc_div1 = 20
Pre = 4e8   ! 工作荷载
! 移动工作平面，创建弧形圆柱体
wpoffs,,0.5*h
cyl4,,,r1,0,r2,180,d  !cy14命令创建圆柱，前两个参数是指定圆心，这里默认是在工作平面上
wpoffs,-r2
blc4,,,r2 - r1,-0.5*h,d
! 工作平面归位
wpcsys,-1
vsel,s,loc,y,0,0.5*h
! 第二个参数1代表移动，2代表复制
vgen,2,all,,,r2 + r1
! 选择全部
allsel,all
! 对称
vsymm,y,all
! 粘接
vglue,all
! 划分网格
! x方向
lsel,s,loc,y,0.5*h
lsel,a,loc,y,-0.5*h
lsel,r,loc,z,0
lesize,all,,,div1 
! y方向
lsel,s,loc,x,r1
lsel,a,loc,x,r2
lsel,a,loc,x,-r1
lsel,a,loc,x,-r2
lsel,r,loc,z,0
lesize,all,,,div2*0.5 
! 半弧
lsel,s,loc,y,0.5*h + 0.01,0.5*h + r2
lsel,a,loc,y,- 0.5*h - 0.01,- 0.5*h - r2
lsel,r,loc,z,0
lesize,all,,,arc_div1
allsel,all
! z方向
! lsel,a,loc,z,0 + 0.1,3.4 - 0.1
esize,h/div2/4
allsel,all
! 划分
vsweep,all
! =======================
! 受力分析
! ! ! 第一种选中点的方法
! ! 选择里圈半弧受力节点
! asel,s,,,4,22,22 - 4
! ! 选中面中所有的点 -- 最后面的参数1代表带边上和其他面一起共用的点一起选中
! nsla,s,1
! ! 第二种选中点的方法
! 建立局部坐标系---工作平面会自动平移到这里
! 第一个局部坐标系
local,11,1,0,0.5*h    ! 参数顺序的意思是11是定义坐标系的编号，1代表柱坐标系，后面三个参数是x,y,z
nsel,s,loc,x,r1
! 第二个局部坐标系
local,12,1,0,-0.5*h
nsel,a,loc,x,r1
! 建立单元
type,2
esurf,all
! 在1类实体单元选中2类单元来进行分析
eplot
esel,s,type,,2
! 施加压力
sfe,all,1,pres,,pre/d
allsel,all


! 固定支座约束
! z方向全部约束
nsel,s,loc,z,d ! 选中Z方向的全部节点
d,all,uz,0      ! z方向约束掉
! 左边固定支座x方向也要约束，右边不用
allsel
! 工作坐标系归位
csys,0
! 先选中左右两边固定约束的节点
nsel,s,loc,y,-0.5*h
! y方向全部约束
d,all,uy,0
! 选中左边的节点
nsel,r,loc,x,-r2,r1-r2
! x方向全部约束
d,all,ux,0
! ==================
! 求解
/solu
! 指定静力分析
antype,0
allsel,all
solve
! ============求解完成进行后处理
! 图形界面操作

! 然后进入后处理来获取最大最小位移
/post1
! 先排序节点排序nsort命令---0是倒序排列
nsort,u,x,0
! 获取节点---最大节点
*get,dx,sort,0,max
! 获取最大节点的编号--位置
*get,dx,sort,0,imax
! 选中节点
nsel,s,,,8590
! 和原来的作比较--变形云图用命令：pldisp
pldisp,1